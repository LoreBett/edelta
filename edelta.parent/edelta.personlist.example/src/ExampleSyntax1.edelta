import edelta.refactorings.lib.EdeltaRefactorings

// IMPORTANT: ecores must be in a source directory
// otherwise you can't refer to them
package com.example

metamodel "PersonList"
metamodel "ecore"

use EdeltaRefactorings as refactorings

changeEClass PersonList.Person {
	refactorings.
		introduceSubclasses(
			it,
			ecoreref(Person.gender),
			ecoreref(Gender)
		);
	EStructuralFeatures+=
		refactorings.mergeAttributes("name",
			ecoreref(Person.firstname).EAttributeType,
			#[ecoreref(Person.firstname),ecoreref(Person.lastname)]
		);
}

createEClass Place in PersonList {
	abstract = true
	refactorings.extractSuperclass(it,
		#[ecoreref(LivingPlace.address), ecoreref(WorkPlace.address)]);
}

createEClass WorkingPosition in PersonList {
	createEAttribute description type EString {}
	refactorings.extractMetaClass(it,ecoreref(Person.works),"position","works");
}

changeEClass PersonList.List {
	EStructuralFeatures+=
		refactorings.mergeReferences("places",
			ecoreref(Place),
			#[ecoreref(List.wplaces),ecoreref(List.lplaces)]
		);
}