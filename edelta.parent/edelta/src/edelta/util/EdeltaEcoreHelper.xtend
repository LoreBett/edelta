/*
 * generated by Xtext 2.10.0
 */
package edelta.util

import com.google.inject.Inject
import edelta.edelta.EdeltaProgram
import edelta.resource.EdeltaDerivedStateComputer
import org.eclipse.emf.ecore.EClass
import org.eclipse.emf.ecore.EClassifier
import org.eclipse.emf.ecore.EDataType
import org.eclipse.emf.ecore.EPackage
import org.eclipse.xtext.util.IResourceScopeCache

/**
 * Helper methods for accessing Ecore elements.
 * 
 * Computations that require filtering or mapping are cached in the scope
 * of the resource.
 * 
 * @author Lorenzo Bettini
 */
class EdeltaEcoreHelper {

	@Inject IResourceScopeCache cache
	@Inject extension EdeltaDerivedStateComputer

	/**
	 * If the passed EPackage is not null then it only returns the
	 * classifiers of that package, otherwise it returns all the
	 * available classifiers in the current program, including the
	 * created ones by the derived state computer.
	 */
	def getEClassifiers(EdeltaProgram context, EPackage epackage) {
		getEClassifiers(context, epackage, null)
	}

	def getEClasses(EdeltaProgram context, EPackage epackage) {
		getEClassifiers(context, epackage, EClass)
	}

	def getEDataTypes(EdeltaProgram context, EPackage epackage) {
		getEClassifiers(context, epackage, EDataType)
	}

	def private getEClassifiers(EdeltaProgram context,
			EPackage epackage,
			Class<? extends EClassifier> kind
	) {
		if (epackage !== null) {
			if (kind !== null)
				return epackage.getEClassifiers.filter(kind)
			return epackage.getEClassifiers
		}
		return 
			switch (kind) {
				case kind === EClass: getEClasses(context)
				case kind === EDataType: getEDataTypes(context)
				default: getEClassifiers(context)
			}
	}

	def private getEClassifiers(EdeltaProgram prog) {
		cache.get("getEClassifiers", prog.eResource) [
			// we also must explicitly consider the derived EPackages
			// created by our derived state computer, containing EClasses
			// created in the program
			(
				prog.eResource.derivedEPackages.
					map[getEClassifiers].
					flatten
			+
				prog.metamodels.
					map[getEClassifiers].
					flatten
			).toList
		]
	}

	def private getEClasses(EdeltaProgram context) {
		cache.get("getEClasses", context.eResource) [
			getEClassifiers(context).filter(EClass).toList
		]
	}

	def private getEDataTypes(EdeltaProgram context) {
		cache.get("getEDataTypes", context.eResource) [
			getEClassifiers(context).filter(EDataType).toList
		]
	}

//	def private getEStructuralFeatures(EObject context) {
//		cache.get("getEStructuralFeatures", context.eResource) [
//			getEClasses(context).map[
//				getEStructuralFeatures
//			].flatten.toList
//		]
//	}

}
